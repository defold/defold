apply plugin: 'java'

apply from: '../com.dynamo.cr.bob/basic.gradle'

allprojects {
    tasks.withType(Jar) {
        exclude('tmp/**')
    }
}

repositories {
    flatDir {
        dirs 'lib', "$project.commonDir/ext", "$project.bobDir/lib"
    }
}

dependencies {
    implementation fileTree(dir: "$project.bobDir/lib", include: '*.jar')
    implementation fileTree(dir: "$project.commonDir/ext", include: [
            'protobuf-java*.jar', 'commons*.jar', 'vecmath.jar',
            'extender-client-*.jar', 'snakeyaml-1.24.jar', 'jsoup-1.11.3.jar', 'aws/*.jar'
    ])
}

task cleanDirs(type: Delete) {
    delete project.classesDir
    delete file("$project.bobDir/generated")
    delete file("$project.bobDir/dist")
    delete file("$project.bobDir/tmp")
    delete file("$project.bobDir/target")
    delete file("$project.bobDir/lib/luajit-share.zip")
}

clean.dependsOn cleanDirs

gradle.projectsEvaluated {
    project.protoFiles.each { proto ->
        def protoDir = file(proto.dir)
        def protoFilePath = file("${proto.dir}/${proto.file}")
        def protoTaskName = "protoTask_${proto.file.replace('/', '_').replace('.', '_')}"
        tasks.create(name: protoTaskName, type: Exec) {
            group = 'build'
            doFirst {
                if (!protoDir.exists()) {
                    throw new GradleException("Directory '${protoDir}' does not exist")
                }
                if (!protoFilePath.exists()) {
                    throw new GradleException("File '${protoFilePath}' does not exist")
                }
                mkdir 'generated'
                if (!project.hostPlatform) {
                    throw new GradleException("hostPlatform is not set. Make sure 'getHostPlatform' task is executed.")
                }
                commandLine "${project.dynamoHome}/ext/bin/${project.hostPlatform}/protoc", "--java_out=generated",
                        "-I${protoDir}", "-I${project.dynamoHome}/ext/include", "-I../../engine/gameobject/proto",
                        "-I../../engine/script/src", "-I../../engine/ddf/src", "-I../../engine/graphics/proto", "${protoFilePath}"
            }
        }
    }
}

task execProtoTasks {
    dependsOn project.protoFiles.collect { proto ->
        "protoTask_${proto.file.replace('/', '_').replace('.', '_')}"
    }
}

task proto(type: Exec){
    group = 'build'
    def protoDir = project.findProperty('protoDir') ?: ''
    def protoFile = project.findProperty('protoFile') ?: ''
    doFirst {
        if (!protoDir || !protoFile) {
            throw new GradleException("Properties 'protoDir' and 'protoFile' must be specified")
        }
        mkdir 'generated'
    }
    doLast {
        if (!project.hostPlatform) {
            throw new GradleException("hostPlatform is not set. Make sure 'getHostPlatform' task is executed.")
        }
        commandLine "${project.dynamoHome}/ext/bin/${project.hostPlatform}/protoc", "--java_out=generated",
                "-I${protoDir}", "-I${project.dynamoHome}/ext/include", "-I../../engine/gameobject/proto",
                "-I../../engine/script/src", "-I../../engine/ddf/src", "-I../../engine/graphics/proto", "${protoDir}/${protoFile}"
    }
}

sourceSets {
    main {
        java {
            srcDirs = ["$project.bobDir/src", 'generated']
        }
    }
}

task compileGenerated(type: JavaCompile) {
    dependsOn execProtoTasks
    source = fileTree('generated')
    destinationDirectory = project.classesDir
    include '**/*.java'
    classpath = sourceSets.main.compileClasspath
    options.encoding = 'UTF-8'
    options.compilerArgs << '-g'
}

task compileSrc(type: JavaCompile, dependsOn: compileGenerated) {
    source = sourceSets.main.java
    destinationDirectory = project.classesDir
    include '**'
    classpath = sourceSets.main.compileClasspath
    options.encoding = 'UTF-8'
    options.compilerArgs << '-g'
}

task createLuaJitShareZip(type: Zip) {
    includeEmptyDirs = false
    archiveFileName = 'luajit-share.zip'
    destinationDirectory = file("$project.bobDir/lib")
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    from "${project.dynamoHome}/ext/share/luajit"

    dependsOn compileSrc, compileGenerated
}

task compileBobLight(dependsOn: [getGitRevision, compileSrc, createLuaJitShareZip]) {
    doLast {
        mkdir project.classesDir
        exec {
            commandLine 'python', "${project.bobDir}/engine_version_generator.py"
        }
        copy {
            includeEmptyDirs = false
            from fileTree("${project.dynamoHome}/share") {
                include 'java/dlib.jar'
                include 'java/modelimporter.jar'
                include 'java/texturecompiler.jar'
            }
            into "$project.bobDir/lib"
        }
        ['dlib.jar', 'modelimporter.jar', 'texturecompiler.jar'].each { jar ->
            if (!file("$project.bobDir/lib/$jar").exists()) {
                throw new GradleException("$jar is missing")
            }
        }
        ['x86_64-macos', 'arm64-macos', 'x86_64-linux', 'x86_64-win32', 'x86-win32'].each { platform ->
            copy {
                includeEmptyDirs = false
                from fileTree("${project.dynamoHome}/ext/bin/$platform")
                into "$project.bobDir/libexec/$platform"
                include '*'
            }
        }
        copy {
            includeEmptyDirs = false
            from tarTree(resources.gzip(file("../../packages/luajit-2.1.0-6c4826f-x86_64-linux.tar.gz")))
            into "$project.bobDir/libexec"
            include 'bin/x86_64-linux/**'
        }
        copy {
            includeEmptyDirs = false
            from tarTree(resources.gzip(file("../../packages/luajit-2.1.0-6c4826f-x86_64-win32.tar.gz")))
            into "$project.bobDir/libexec"
            include 'bin/x86_64-win32/**'
        }
        copy {
            includeEmptyDirs = false
            from tarTree(resources.gzip(file("../../packages/luajit-2.1.0-6c4826f-x86_64-macos.tar.gz")))
            into "$project.bobDir/libexec"
            include 'bin/x86_64-macos/**'
        }
        copy {
            includeEmptyDirs = false
            from tarTree(resources.gzip(file("../../packages/luajit-2.1.0-6c4826f-arm64-macos.tar.gz")))
            into "$project.bobDir/libexec"
            include 'bin/arm64-macos/**'
        }
        copy {
            includeEmptyDirs = false
            from fileTree("$project.bobDir/src") {
                exclude '**/*.java'
            }
            into project.classesDir
        }
        copy {
            includeEmptyDirs = false
            from fileTree("${project.dynamoHome}/lib") {
                include 'x86_64-macos/*texc_shared*'
                include 'x86_64-macos/*modelc_shared*'
                exclude '**/*dSYM.zip'
            }
            into "$project.bobDir/lib/x86_64-macos"
        }
        copy {
            includeEmptyDirs = false
            from fileTree("${project.dynamoHome}/lib") {
                include 'arm64-macos/*texc_shared*'
                include 'arm64-macos/*modelc_shared*'
            }
            into "$project.bobDir/lib/arm64-macos"
        }
        copy {
            includeEmptyDirs = false
            from fileTree("${project.dynamoHome}/lib") {
                include 'x86_64-linux/*texc_shared*'
                include 'x86_64-linux/*modelc_shared*'
                exclude '**/*dSYM.zip'
            }
            into "$project.bobDir/lib/x86_64-linux"
        }
        copy {
            includeEmptyDirs = false
            from fileTree("${project.dynamoHome}/lib") {
                include 'win32/*texc_shared*'
                include 'win32/*modelc_shared*'
                exclude '**/*dSYM.zip'
            }
            into "$project.bobDir/lib/x86-win32"
        }
        copy {
            includeEmptyDirs = false
            from fileTree("${project.dynamoHome}/lib") {
                include 'x86_64-win32/*texc_shared*'
                include 'x86_64-win32/*modelc_shared*'
                exclude '**/*dSYM.zip'
            }
            into "$project.bobDir/lib/x86_64-win32"
        }
    }
}

task compile(dependsOn: ['compileBobLight']) {
    doLast {
        jar {
            includeEmptyDirs = false
            archiveFileName = 'android-res.zip'
            from "${project.dynamoHome}/ext/share/java/res"
        }
        copy {
            includeEmptyDirs = false
            from fileTree("${project.dynamoHome}/ext/lib") {
                include 'win32/OpenAL32.dll'
                include 'win32/wrap_oal.dll'
            }
            into "$project.bobDir/lib/x86-win32"
        }
        copy {
            includeEmptyDirs = false
            from fileTree("${project.dynamoHome}/ext/lib") {
                include 'x86_64-win32/OpenAL32.dll'
                include 'x86_64-win32/wrap_oal.dll'
            }
            into "$project.bobDir/lib/x86_64-win32"
        }
    }
}

task createExternalLibsLightJar(type: Jar) {
    includeEmptyDirs = false
    archiveFileName = 'external-libs-light.jar'
    destinationDirectory = file("$project.bobDir/tmp")
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    from {
        fileTree(dir: "$project.commonDir/ext", include: [
                'protobuf-java*.jar', 'commons*.jar', 'vecmath.jar',
                'extender-client-*.jar', 'snakeyaml-1.24.jar', 'jsoup-1.11.3.jar',
                'httpcore-4.1.4.jar'
        ]).collect { zipTree(it) }
    }
    from {
        fileTree(dir: "$project.bobDir/lib", include: '*.jar', exclude: 'android.jar').collect { zipTree(it) }
    }

    dependsOn compileSrc

    doLast {
        if (!file("$project.bobDir/tmp/external-libs-light.jar").exists()) {
            throw new GradleException("File '$project.bobDir/tmp/external-libs-light.jar' was not created")
        } else {
            println "external-libs-light.jar successfully created"
        }
    }
}

task createBobLightJar(type: Jar, dependsOn: createExternalLibsLightJar) {
    includeEmptyDirs = false
    archiveFileName = 'bob-light.jar'
    destinationDirectory = file("$project.bobDir/dist")
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    from project.classesDir
    from fileTree("$project.bobDir") {
        include 'lib/**/*texc_shared*'
        include 'lib/**/*modelc_shared*'
        exclude '**/*dSYM.zip'
        exclude 'tmp/**'
    }
    from fileTree("$project.bobDir") {
        include 'libexec/**/glslc*'
        include 'libexec/**/spirv-cross*'
        include 'libexec/**/spirv-opt*'
        include 'libexec/**/luajit*'
    }

    from {
        zipTree(file("$project.bobDir/tmp/external-libs-light.jar"))
    } {
        exclude '**/META-INF/**'
    }
    from file("$project.bobDir/lib/luajit-share.zip")

    manifest {
        attributes 'Main-Class': 'com.dynamo.bob.Bob', 'is-bob-light': 'true'
    }
}

task distBobLight(dependsOn: ['compileBobLight', 'createBobLightJar']) {
    doLast {
        mkdir "$project.bobDir/dist"
        mkdir "$project.bobDir/tmp"
    }
}

task createExternalLibsBuiltinsJar(type: Jar) {
    includeEmptyDirs = false
    archiveFileName = 'external-libs-builtins.jar'
    destinationDirectory = file("$project.bobDir/tmp")
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    from {
        fileTree(dir: "$project.commonDir/ext", include: [
                'protobuf-java*.jar', 'commons*.jar', 'vecmath.jar',
                'extender-client-*.jar', 'snakeyaml-1.24.jar', 'jsoup-1.11.3.jar'
        ]).collect { zipTree(it) }
    }
    from {
        fileTree(dir: "$project.commonDir/ext/aws", include: '*.jar').collect { zipTree(it) }
    }
    from {
        fileTree(dir: "$project.bobDir/lib", include: '*.jar', exclude: 'android.jar').collect { zipTree(it) }
    }

    dependsOn compileSrc
    dependsOn compileGenerated

    doLast {
        if (!file("$project.bobDir/tmp/external-libs-builtins.jar").exists()) {
            throw new GradleException("File '$project.bobDir/tmp/external-libs-builtins.jar' was not created")
        } else {
            println "external-libs-builtins.jar successfully created"
        }
    }
}

task createBobJar(type: Jar, dependsOn: [createExternalLibsBuiltinsJar, createLuaJitShareZip]) {
    includeEmptyDirs = false
    archiveFileName = 'bob.jar'
    destinationDirectory = file("$project.bobDir/dist")
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    from project.classesDir
    from fileTree("$project.bobDir") {
        include 'lib/**/*texc_shared*'
        include 'lib/**/*modelc_shared*'
        include 'lib/**/*libc++*'
        include 'lib/*win32/OpenAL32.dll'
        include 'lib/*win32/wrap_oal.dll'
        exclude '**/*dSYM.zip'
        exclude 'tmp/**'
    }
    from fileTree("$project.bobDir") {
        include 'libexec/**/*'
    }
    from fileTree("$project.bobDir") {
        include 'lib/report_template.html'
        include 'lib/time_report_template.html'
    }

    from {
        zipTree(file("$project.bobDir/tmp/external-libs-builtins.jar"))
    } {
        exclude '**/META-INF/**'
    }

    from {
        zipTree(file("$project.bobDir/lib/builtins.zip"))
    }

    from fileTree("$project.bobDir") {
        include 'lib/android.jar'
        include 'lib/classes.dex'
        include 'lib/*.zip'
        exclude 'tmp/**'
    }

    manifest {
        attributes 'Main-Class': 'com.dynamo.bob.Bob',
                   'Gradle-Version': gradle.gradleVersion,
                   'Created-By': "${System.getProperty('java.version')} (${System.getProperty('java.vendor')})"
    }
}

task distBob(dependsOn: ['compile', 'createBobJar']) {
    doLast {
        mkdir "$project.bobDir/dist"
        mkdir "$project.bobDir/tmp"
    }
}

task installBobLight(dependsOn: ['distBobLight']) {
    doLast {
        copy {
            from "$project.bobDir/dist/bob-light.jar"
            into "$project.dynamoHome/share/java"
        }
    }
}

task install(dependsOn: ['distBob']) {
    doLast {
        copy {
            from "$project.bobDir/dist/bob.jar"
            into "$project.dynamoHome/share/java"
        }
    }
}
